plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.17.0'
}

version = '0.0.2'

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    api 'org.jline:jline:3.29.0'

    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-text:1.13.0'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}


test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.klakpin'
            artifactId = 'diva-term'

            from components.java

            pom {
                name = 'Diva Term'
                description = 'Set of simple ready-to-use terminal UI components for interactive Java CLI applications'
                url = 'https://github.com/aalmiray/app'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'klakpin'
                        name = 'Ilia Kuznetsov'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/klakpin/DivaTerm.git'
                    developerConnection = 'scm:git:ssh://github.com/klakpin/DivaTerm.git'
                    url = 'https://github.com/klakpin/DivaTerm'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    gitRootSearch = true

    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    retryDelay = 30
                    maxRetries = 60
                }
            }
        }
    }
}